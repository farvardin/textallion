! by AURAES: http://auraes.free.fr/ldveh/
!
! Encodage ISO-8859-1
! version Beta 0.96 Parchment
! ------------------------------------------------------------------------------

Constant DE1          6; ! 6 faces
Constant DE1_CHANCE   5; ! si >= 5 == Chance

Array facesDe-> 1 2 3 4 5 6; ! contenu faces

! ------------------------------------------------------------------------------
[ DeColor face colorT colorB;
	@set_colour colorT colorB;
	print "[", face, "]";
	style roman;
	@set_colour CLR_DEFAULT CLR_DEFAULT;
	style reverse;
	print " ";
	style roman;
];

[ MelangeDe   i rnd xchg;
	i = DE1 - 1;
	while( i ) {
		rnd = random(i + 1) - 1;
		xchg = facesDe-> i;
		facesDe-> i = facesDe-> rnd;
		facesDe-> rnd = xchg;
		i--;
	}
];

[ AfficheDes deChoix   i width;
	@set_window 1;
	width = ScreenWidth() - (DE1 * 4 - 1);
	font off;
	MoveCursor(1, width);
	for ( i = 0 : i < DE1 : i++ ) {
		if( deChoix ) {
			if( i ~= (deChoix - 1) )
				DeColor(facesDe-> i, CLR_BLACK, CLR_YELLOW);
			else { 
				if( facesDe-> i >= DE1_CHANCE )
					DeColor(facesDe-> i, CLR_WHITE, CLR_BLUE);
				else DeColor(facesDe-> i, CLR_WHITE, CLR_RED);
			}
		}
		else DeColor(i + 1, CLR_BLACK, CLR_YELLOW);
	}
	@set_window 0;
	font on;
];

[ TestChance;
	new_line;
	new_line;	
	spaces ESPACE_RETRAIT;	
	print "Tentez votre Chance";
	if( flagDe )
		print ", choisissez une des six faces du dé";
	print " !";
	flagDe = 0;	
	MelangeDe();
	AfficheDes();
	@push nbrChoixPage;
	nbrChoixPage = DE1;
	Keyboard();
	@pull nbrChoixPage;
	AfficheDes(choix1a9);
	if ( facesDe-> (choix1a9 - 1) < DE1_CHANCE ) rfalse;
];

! ------------------------------------------------------------------------------

